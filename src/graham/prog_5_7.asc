10 REM *
20 REM * PROG 5.7 - Insert, Delete, Replace
30 REM *
40 KEY OFF
50 SCREEN 0
60 WIDTH 38
70 CLEAR 400
80 PRINT "String Editing"
90 PRINT "--------------"
100 PRINT
110 PRINT "Type in text :"
120 INPUT TEXT$
130 L=LEN(TEXT$)
140 IF L=0 THEN GOTO 120
150 REM *
160 REM * Main Loop
170 REM *
180 PRINT
190 PRINT "-------------"
200 PRINT "1- Insert":PRINT "2- Delete":PRINT "3 - Replace"
210 PRINT "-------------"
220 PRINT
230 PRINT "Text: ";TEXT$ : PRINT
240 INPUT "Option";O
250 IF O<1 OR O>3 THEN GOTO 200
260 PRINT
270 ON O GOSUB 320,470,600
280 GOTO 190
290 REM *
300 REM * Insert Routine
310 REM *
320 INPUT "Insert Text:";I$
330 IL=LEN(I$) : IF IL=0 THEN 320
340 IF IL+L>255 THEN PRINT "* No Room": RETURN
350 PRINT
360 INPUT "Insert at position ";P
370 IF P<1 OR P>LEN(TEXT$) THEN 360
380 PRINT
390 IF P>L+1 THEN GOTO 360
400 A$ =LEFT$(TEXT$,P-1)
410 B$ =RIGHT$(TEXT$,L-P+1)
420 TEXT$=A$ + I$ + B$
430 L = LEN(TEXT$)
440 RETURN
450 REM *
460 REM * Delete Routine
470 REM *
480 INPUT "Delete Text:";D$
490 PRINT
500 DL=LEN(D$) : IF DL=0 THEN 480
510 P =INSTR(TEXT$,D$)
520 IF P=0 THEN PRINT "* Not found" : PRINT : RETURN
530 A$=LEFT$(TEXT$,P-1)
540 B$=RIGHT$(TEXT$,L-P-DL+1)
550 TEXT$=A$+B$
560 L=LEN(TEXT$)
570 RETURN
580 REM  *
590 REM * Replace Routine
600 REM *
610 INPUT "Search Text:";S$
620 SL=LEN(S$) : IF SL=0 THEN 610
630 P = INSTR(TEXT$,S$)
640 IF P=0 THEN PRINT "* Not found" : PRINT: RETURN
650 PRINT
660 INPUT "Replace Text:";R$
670 RL=LEN(R$) : IF RL=0 THEN 780
680 PRINT
690 REM *
700 REM * Replace String=in length
710 REM *
720 IF RL=SL THEN MID$(TEXT$,P,RL)=R$ :RETURN
730 IF RL>SL GOTO 850
740 REM *
750 REM * Replace Long With Short
760 REM * String
770 REM *
780 MID$(TEXT$,P,RL)=R$
790 P=P+RL : DL=SL-RL
800 GOSUB 530 : RETURN
810 REM *
820 REM * Replace Short With Long
830 REM * String
840 REM *
850 B = RL-SL
860 IF B+L>255 THEN PRINT "* No room":RETURN
870 MID$(TEXT$,P,SL)=R$
880 P = P+SL
890 A$ = LEFT$(TEXT$,P-1)
900 B$=RIGHT$(TEXT$,L-P+1)
910 TEXT$=A$+RIGHT$(R$,B)+B$
920 L = LEN(TEXT$)
930 RETURN
