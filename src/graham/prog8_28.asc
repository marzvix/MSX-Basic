10 REM *************************************
20 REM *                                   *
30 REM * PROG 8.28 - Graph Drawing Program *
40 REM *                                   *
50 REM *************************************
60 REM *
70 GOTO 200
80 REM *
100 REM *
110 REM * Input Scrolling Routine
120 REM *
130 LOCATE 0,4,0
140 FOR I = ITEM-18 TO ITEM-1
150 PRINT USING "###";I;:PRINT TAB(7);TBL(I)
160 NEXT I
170 RETURN
180 LOCATE 0,22
200 REM *
210 REM * Data Input Section
220 REM *
230 SCREEN 0
240 OPEN "GRP:" FOR OUTPUT AS #1
250 PLAY "T255L12SM2000"
260 COLOR 15,4,4 : KEY OFF
270 PRINT "Data Input:" : PRINT
280 CLS
300 PRINT "Maximum of 110 Data Items (*exits)." : PRINT
310 ITEM = 1 : MX=0 : MIN=0
320 PRINT USING "###";ITEM;: PRINT ":"; TAB(8)
330 LINE INPUT A$
340 REM *
350 REM *Terminate Data input if "*"
360 REM *
370 IF A$="*" THEN GOTO 570
380 X=VAL(A$)
390 REM *
400 REM * Fill Array and Determine
410 REM *  Max. and Min. Data Values
420 REM *
430 TBL(ITEM)=X : IF X>MX THEN MX=X
440 IF X<MIN THEN MIN = X
450 ITEM = ITEM + 1
460 REM *
470 REM * If Screen Full - Scroll
480 REM * Display
490 REM *
500 IF ITEM>19 THEN GOSUB 130
510 IF ITEM < 111 THEN GOTO 320
520 PRINT
530 PRINT"Array Full: Press key for menu." : X$ = INPUT$(1)
540 REM *
550 REM * Main Menu
560 REM *
570 SCREEN 0
580 PRINT TAB (1) ;"Graph Drawing:"
590 PRINT : PRINT
600 PRINT TAB(8);"1. Bar Chart" : PRINT
610 PRINT TAB(8);"2. Line Graph" :PRINT
620 PRINT TAB(8);"3. Scatter Diagram":PRINT
630 PRINT TAB(8);"4. Data Entry":PRINT
640 PRINT TAB(8);"5. Exit Program":PRINT
650 PRINT TAB(8);"Input Option (1-5):)";
651 LOCATE 2,20,0: PRINT "* Press Space Bar to Return to Menu"
660 BL$ = "" : BC$ = "_"
670 LOCATE 29,13,0
680 A$=INKEY$
690 IF A$<>"" THEN 740
700 SWAP BL$,BC$
710 PRINT BL$;
720 FOR D = 1 TO 100 : NEXT D
730 GOTO 670
740 PRINT A$
750 REM *
760 REM * Validate Input
770 REM *
780 IF A$<"1" OR A$>"5" THEN PLAY "O2F": LOCATE 29,13: PRINT " " :GOTO 670
790 PLAY "O4CO6C"
800 PRINT : PRINT
810 PRINT TAB(8);"Option ";A$;" Selected."
820 FOR D = 1 TO 500 : NEXT
830 SEL = VAL(A$)
840 IF SEL = 4 THEN GOTO 280
850 IF SEL = 5 THEN CLS : END
860 CLS
870 COLOR 15,4,4
880 SCREEN 2
890 REM *
900 REM * Graph Calculations
910 REM * Determine Scaling Factor
920 REM *
930 R = ABS(MX)+ABS(MIN)
940 IF R = 0 THEN GOTO 570
950 FTR = 176/R
960 IF NOT(SGN(MIN)) THEN OG = 183 : GOTO 1010
970 REM *
980 REM *Determine Origin and Draw Axes
990 REM *
1000 OG =183 + (MIN*FTR)
1010 LINE (15,OG)-STEP(240,0)
1020 LINE (15,0)-STEP(0,191)
1030 CIRCLE(8,OG),2
1040 REM *
1050 REM * Scale Y Axis^10
1060 REM *
1070 GOSUB 1560
1080 IF OG = 183 THEN GOTO 1130
1090 PSET (16,OG)
1100 FOR I = 0 TO INT(ABS(MIN)/10^P)
1110 LINE (12,OG+(FTR*(10^P)*I))-STEP(3,0)
1120 NEXT
1130 PSET (16,OG)
1140 FOR I = 0 TO INT(MX/10^P)
1150 LINE (12,OG-(FTR*(10^P)*I))-STEP(3,0)
1160 NEXT
1170 REM *
1180 REM * Determine Step Width
1190 REM * for the X axis
1200 REM *
1210 STP =INT(224/(ITEM-1))
1220 REM *
1230 REM * Graph Selection and Drawing
1240 REM *
1250 ON SEL GOTO 1290,1390,1490
1260 REM *
1270 REM * Draw Bar Chart
1280 REM *
1290 SP = 15 : PSET(SP,OG)
1300 FOR I = 1 TO ITEM-1
1310 LINE (SP,OG)-STEP(STP-2,-(TBL(I)*FTR)),,BF
1320 SP = SP + STP
1330 NEXT I
1340 FOR D=1 TO 500:NEXT
1350 X$ = INPUT$(1) : GOTO 570
1360 REM *
1370 REM * Draw Line Graph
1380 REM *
1390 SP = 15 : PSET(SP,OG)
1400 FOR I = 1 TO ITEM-1
1410 LINE -(SP,OG-(TBL(I)*FTR))
1420 SP = SP + STP
1430 NEXT I
1440 FOR D=1 TO 500:NEXT
1450 X$ = INPUT$(I) : GOTO 570
1460 REM *
1470 REM * Draw Scatter Diagram
1480 REM *
1490 SP = 20 : PSET(SP,OG)
1500 FOR I = 1 TO ITEM-1
1510 CIRCLE(SP,OG-(TBL(I)*FTR)),1
1520 SP = SP + STP
1530 NEXT I
1540 X$ = INPUT$(I) : GOTO 570
1550 FOR D=1 TO 500=NEXT
1560 REM *
1570 REM * Determine the Scaling
1580 REM * for the Y Axis ^ 10
1590 REM *
1600 P=4
1610 IF R<10 THEN P = 0 : RETURN
1620 IF INT (R/10^P) = 0 THEN P = P-1:
1630 RETURN
