10 REM *************************************
20 REM *                                   *
30 REM * PROG 3.8 - Simple Payroll Program *
40 REM *                                   *
50 REM *************************************
60 REM *
70 REM * Main Program
80 REM *
90 SCREEN 0 : KEY OFF
100 WIDTH 38
110 PRINT "Type in the number of"
120 PRINT "employees to be processed."
130 INPUT ":";N%
140 IF NY% < 0 THEN BEEP : GOTO 130
150 REM *
160 REM * Main Loop
170 REM * 
180 GOSUB 320 : REM * Input Data
190 GOSUB 420 : REM * Overtime Calculation
200 GOSUB 500 : REM * Tax Calculation
210 GOSUB 590 : REM * Printout Routine
220 N% = N%-1
230 REM *
240 REM *  If all workers processed, finish
250 REM * 
260 IF N%=0 THEN PRINT "Finished": END
270 INPUT "Press Return For Next Item";A$
280 GOTO 180
290 REM *
300 REM * Input Subroutine
310 REM *
320 CLS
330 INPUT "Name             : ";N$
340 INPUT "Hours Worked     : ";HW
350 IF (HW<0) OR (HW>50) THEN BEEP : GOTO 340
360 INPUT "Rate of Pay/Hour : ";RP
370 IF (RP<0) THEN BEEP : GOTO 360
380 RETURN
390 REM *
400 REM * Overtime Calculation
410 REM *
420 IF (HW<=37) THEN OH=0:OP=0 : RETURN
430 OH = HW-37
440 HW = 37
450 OP = (1.5 * RP) * OH
460 RETURN
470 REM *
480 REM * Pay and Tax Calculation
490 REM *
500 BP = (HW * RP)
510 GP = BP + OP
520 IF GP<=40 THEN TAX=0:NP=GP:RETURN
530 TAX = GP * .3
540 NP = GP - TAX
550 RETURN
560 REM *
570 REM * Output All Workers Details
580 REM *
590 CLS
600 PRINT "Final Details"
610 PRINT
620 PRINT "Name              :";N$
630 PRINT
640 PRINT "Hours Worked      :";HW
650 PRINT "Rate of Pay       :"; RP
660 PRINT
670 PRINT "Basic Pay         :";BP
680 PRINT "Hours at Overtime :";OH
690 PRINT "Overtime Pay      :";OP
700 PRINT
710 PRINT "Gross Pay         :";GP
720 PRINT "Tax due           :";TAX
730 PRINT
740 PRINT "NET PAY           :";NP
750 PRINT
760 RETURN
